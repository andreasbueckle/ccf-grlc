
PREFIX ccf: <http://purl.org/ccf/>
PREFIX HRAkg: <https://purl.humanatlas.io/collection/hra-kg>

SELECT ?label ?count
FROM HRAkg:
WHERE {
  {
    SELECT ("# Nodes" as ?label) (COUNT(DISTINCT(?s)) as ?count)
    WHERE {
      {
        ?s ?p1 [] .
      }
      UNION
      {
        [] ?p2 ?s .
        FILTER(isIRI(?s)) 
      }
    }
  }
  UNION
  {
    SELECT ("# Edges" as ?label) (COUNT(*) as ?count)
    WHERE {
      ?s ?p ?o .
    }
  }
  UNION
  {
    SELECT ("# Edge Types" as ?label) (COUNT(DISTINCT(?p)) as ?count)
    WHERE {
      ?s ?p ?o .
    }
  }
}
