#+ summary: AS-CT Edges (unique)

PREFIX ccf: <http://purl.org/ccf/>

SELECT (GROUP_CONCAT(?organ;SEPARATOR='|') as ?organ)
  ?source ?target
  (SAMPLE(?source_type) as ?source_type)
  (SAMPLE(?target_type) as ?target_type)
WHERE {
  {
    SELECT ?organ ?source ?target
      (SAMPLE(?source_type) as ?source_type)
      (SAMPLE(?target_type) as ?target_type)
    WHERE {
      GRAPH ?graph {
        {
          ?source ccf:ccf_part_of ?target .
        }
        UNION
        {
          ?source ccf:ccf_located_in ?target .
        }

        ?source ccf:ccf_asctb_type ?source_type .
        FILTER(?source_type != 'BM')

        ?target ccf:ccf_asctb_type ?target_type .
        FILTER(?target_type != 'BM')
      }
      FILTER(STRSTARTS(STR(?graph), 'https://purl.humanatlas.io/asct-b/'))
      BIND(REPLACE(REPLACE(STR(?graph), 'https://purl.humanatlas.io/asct-b/', ''), '-', '_') as ?organ)
      FILTER(?source != ?target)
    }
    GROUP BY ?organ ?source ?target
  }
}
GROUP BY ?source ?target
