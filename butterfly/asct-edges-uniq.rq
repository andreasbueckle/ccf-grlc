#+ summary: AS-CT Edges (unique)

PREFIX ccf: <http://purl.org/ccf/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX HRA: <https://purl.humanatlas.io/collection/hra-api>

SELECT (GROUP_CONCAT(?organ;SEPARATOR='|') as ?organ)
  ?source ?target
  (SAMPLE(?source_type) as ?source_type)
  (SAMPLE(?target_type) as ?target_type)
WHERE {
  hint:Query hint:optimizer "Runtime" .

  {
    SELECT ?organ ?source ?target
      (SAMPLE(?source_type) as ?source_type)
      (SAMPLE(?target_type) as ?target_type)
    WHERE {
      {
        SELECT DISTINCT ?graph ?organ
        WHERE {
          GRAPH HRA: {
            HRA: a owl:Ontology ;
              prov:hadMember ?versionedGraph .
            FILTER(STRSTARTS(STR(?versionedGraph), 'https://purl.humanatlas.io/asct-b/'))
          }
          GRAPH ?namedGraph {
            ?graph a owl:Ontology .
            FILTER(STRSTARTS(STR(?graph), 'https://purl.humanatlas.io/asct-b/'))
          }
          FILTER(STRSTARTS(STR(?versionedGraph), STR(?graph)))
          BIND(REPLACE(REPLACE(STR(?graph), 'https://purl.humanatlas.io/asct-b/', ''), '-', '_') as ?organ)
        }
      }

      GRAPH ?graph {
        {
          ?source ccf:ccf_part_of ?target .
        }
        UNION
        {
          ?source ccf:ccf_located_in ?target .
        }
        FILTER(?source != ?target)

        ?source ccf:ccf_asctb_type ?source_type .
        FILTER(?source_type != 'BM')

        ?target ccf:ccf_asctb_type ?target_type .
        FILTER(?target_type != 'BM')
      }
      FILTER(STRSTARTS(STR(?graph), 'https://purl.humanatlas.io/asct-b/'))
    }
    GROUP BY ?organ ?source ?target
  }
}
GROUP BY ?source ?target
