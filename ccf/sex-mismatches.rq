#+ summary: Sex Mismatches
#+ description: Get Tissue Blocks that have a Donor sex different from the Reference Organ sex

PREFIX ccf: <http://purl.org/ccf/>

SELECT ?block ?ruiLocation ?refOrgan ?donorSex ?refOrganSex
WHERE {
  # Find donor sex
  ?block ccf:comes_from [
      ccf:sex ?donorSex
  ] .
  
  # Find reference organ sex associated with the block's rui_location 
  ?block ccf:has_registration_location ?ruiLocation .
  [] ccf:placement_for ?ruiLocation ;
    ccf:placement_relative_to ?refOrgan .
  ?refOrgan ccf:organ_owner_sex ?refOrganSex .

  FILTER ( STR(?donorSex) != STR(?refOrganSex) )
}
