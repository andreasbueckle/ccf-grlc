#+ summary: Sex Mismatches
#+ description: Get Tissue Blocks that have a Donor sex different from the Reference Organ sex

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ccf: <http://purl.org/ccf/latest/ccf.owl#>
PREFIX entity: <http://purl.org/ccf/latest/ccf-entity.owl#>

SELECT ?block ?ruiLocation ?refOrgan ?donorSex ?refOrganSex
WHERE {
  ?block entity:has_donor ?donor .
  ?donor entity:sex ?donorSex .
  ?block entity:has_spatial_entity ?ruiLocation .
  ?place ccf:has_placement_source ?ruiLocation .
  ?place ccf:has_placement_target ?refOrgan .
  ?refOrgan ccf:has_sex ?refOrganSex .
  FILTER ( STR(?donorSex) != STR(?refOrganSex) )
}
